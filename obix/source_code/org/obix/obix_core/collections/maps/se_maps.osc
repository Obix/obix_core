///
   Copyright (C) 2009-2012 Christian Neumanns (www.rps-obix.com)
   This code can be used under the terms of the 'GNU Afero General Public License version 3'
   The full text of this license can be found at http://www.gnu.org/licenses/agpl.html
   THIS CODE IS DISTRIBUTED WITHOUT ANY WARRANTY. See the license for details.
end ///

service maps

	command map_to_long_string
		in map type:string_any_type_mutable_or_immutable_map voidable:yes end
		in item_separator type:string default:se_string_constants.current_OS_new_line end
		in key_value_separator type:string default:": " end

		out result type:string end
		
		script
			if i_map =r void then
				o_result = se_string_constants.void
				exit script
			end if
			
			const mutable_string mutable_string = fa_mutable_string.create

			const !iterator<simple_non_empty_string> c_iterator = i_map.key_iterator

			repeat for each simple_non_empty_string key in c_iterator
				c_mutable_string.append ( se_any_type.force_object_to_string ( c_key ) )
				c_mutable_string.append ( i_key_value_separator )
				c_mutable_string.append ( se_any_type.force_object_to_string ( i_map.require_item ( c_key ) ) )

				if c_iterator.has_next then
					c_mutable_string.append ( i_item_separator )
				end if
			end repeat
			
			o_result = c_mutable_string.to_string
		end
	end

end service
